MODULE Goal;
REQUIRE Game, Player;

CLASS Goal 'Гол';

game 'Матч' = DATA Game (Goal);
gameName 'Матч' (Goal goal) = gameName(game(goal));
player 'Игрок' = DATA Player (Goal);
playerNumber 'Номер Игрока' (Goal goal) = number(player(goal));
playerTeam 'Команда' (Goal goal) = team(player(goal));
playerTeamName 'Команда' (Goal goal) = name(team(player(goal)));

CONSTRAINT playerTeam(goal) != hostTeam(game(goal))
    AND playerTeam(goal) != guestTeam(game(goal))
    MESSAGE 'Автор гола должен принадлежать команде, участвующей в матче';

FORM editGoal 'Гол'
    OBJECTS g = Goal
    PANEL PROPERTIES(g) gameName, playerNumber

    EDIT Goal OBJECT g;

FORM goalList 'Список голов'
    OBJECTS g = Goal
    PROPERTIES(g) READONLY gameName, playerNumber, playerTeamName
    PROPERTIES(g) NEWSESSION NEW, EDIT, DELETE

    LIST Goal OBJECT g;

NAVIGATOR {
    NEW goalList;
}
